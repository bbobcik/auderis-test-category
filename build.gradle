apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group 'cz.auderis'
version '1.0.1'
ext.releaseBuild = !version.endsWith('-SNAPSHOT')

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

if (releaseBuild) {
	if (!LOCAL_CREDENTIALS.sonatype) {
		throw new InvalidUserDataException("Sonatype credentials not defined (check LOCAL_CREDENTIALS.sonatype definition)")
		/**
		 * Recommended setup for credentials: create file ~/.gradle/init.d/credentials.gradle with the following
		 * contents:
		 * <pre>
			 *   rootProject {
		 *     ext.LOCAL_CREDENTIALS = [
		 *       sonatype: [
		 *         userName: '...',
		 *         password: '...'
		 *       ],
		 *     ]
		 *   }
		 * </pre>
		 */
	}
	signing {
		sign configurations.archives
	}
} else {
	task signArchives << {
		logger.info("Non-release build, signing not performed")
	}
}

uploadArchives {
	repositories {
		if (releaseBuild) {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: LOCAL_CREDENTIALS.sonatype.userName, password: LOCAL_CREDENTIALS.sonatype.password)
				}

				pom.project {
					name 'Auderis Test - JUnit categories and other annotations'
					description 'Auderis Test - JUnit annotations, among others definition of general categories for test classification'
					url 'https://github.com/bbobcik/auderis-test-category'
					inceptionYear 2015
					packaging 'jar'

					organization {
						name 'Boleslav Bobcik - Auderis'
					}

					developers {
						developer {
							name 'Boleslav Bobcik'
							email 'bbobcik@gmail.com'
							organization 'Auderis'
							organizationUrl 'https://github.com/bbobcik'
							timezone 1
						}
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					scm {
						url 'git@github.com:bbobcik/auderis-test-category.git'
						connection 'scm:git:git@github.com:bbobcik/auderis-test-category.git'
						developerConnection 'scm:git:git@github.com:bbobcik/auderis-test-category.git'
						tag 'HEAD'
					}
				}
			}
		} else {
			mavenLocal()
		}
	}
}

uploadArchives.doFirst {
	if (releaseBuild) {
		logger.lifecycle("Release build - uploading artifacts to OSS Sonatype Nexus")
	} else {
		logger.lifecycle("Non-release build - uploading artifacts to Maven Local")
	}
}
